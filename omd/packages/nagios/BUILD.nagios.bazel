load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**/*"],
        exclude = ["bazel-*"],
    ),
)

configure_make(
    name = "nagios",
    args = ["-j4"],
    configure_in_place = True,
    configure_options = [
        "--with-nagios-user=$$(id -un)",
        "--with-nagios-group=$$(id -gn)",
        "--with-perlcache",
        "--enable-embedded-perl",
    ],
    copts = [
        # The implicit int warning cannot be fixed with `-std=c89`
        # as the code also uses newer features such as `inline`.
        "-Wno-deprecated-non-prototype",
        "-Wno-implicit-int",
        "-Wno-unused-but-set-variable",
        "-Wl,--rpath,$ORIGIN_VAR/../lib",
    ],
    env = {
        "PKG_CONFIG_PATH": ":".join([
            "/usr/lib/pkgconfig",
            "/usr/lib64/pkgconfig",
            "/usr/lib/x86_64-linux-gnu/pkgconfig",
        ]),
        "ORIGIN_VAR": "\\\\$$\\$$ORIGIN",
    },
    lib_source = ":all_srcs",
    out_binaries = [
        "nagios",
        "nagiostats",
    ],
    out_data_dirs = [
        "share",
        "lib",
    ],
    postfix_script = """
        mkdir -p ${INSTALLDIR}/lib/nagios
        install -m 644 ${BUILD_TMPDIR}/p1.pl ${INSTALLDIR}/lib/nagios

        # Copy package documentations to have these information in the binary packages
        mkdir -p ${INSTALLDIR}/share/doc/nagios
        set -e ; for file in README THANKS LEGAL LICENSE ; do \
            install -m 644 ${BUILD_TMPDIR}/$file ${INSTALLDIR}/share/doc/nagios; \
        done
    """,
    targets = [
        "all",
        "install-base",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "gen_dir",
    srcs = [":nagios"],
    output_group = "gen_dir",
    visibility = ["//visibility:public"],
)

filegroup(
    name = "nagiostats_bin",
    srcs = [":nagios"],
    output_group = "nagiostats",
    visibility = ["//visibility:public"],
)

filegroup(
    name = "nagios_bin",
    srcs = [":nagios"],
    output_group = "nagios",
    visibility = ["//visibility:public"],
)
